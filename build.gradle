buildscript {
    ext {
        ffmpeg_version = '0.6.2'
        slf4j_version = '2.0.0-alpha1'
        logback_version = '1.3.0-alpha5'
        raksrinana_utils_version = '15.0.1'
        commons_math3_version = '3.6.1'
        commons_lang3_version = '3.11'
        picocli_version = '4.6.1'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'com.novoda.build-properties' version '0.4.1'
    id 'idea'
    id 'java'
    id 'application'
    id 'io.freefair.lombok' version '5.3.0'
}

def secretsFile = new File(".", "secrets.properties")

if (!secretsFile.getParentFile().exists()) {
    secretsFile.mkdirs()
}

if (!secretsFile.exists()) {
    secretsFile.withWriterAppend { w ->
        w << "\n"
    }
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or(System.getenv()).or([
                "GITHUB_USER" : "RakSrinaNa",
                "GITHUB_TOKEN": "N/A"
        ])
    }
}

group = 'fr.raksrinana'
description = 'MediaConverter'
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_15

ext.moduleName = project.moduleName
mainClassName = project.mainClass

dependencies {
    implementation("net.bramp.ffmpeg:ffmpeg:${ffmpeg_version}") {
        exclude module: 'commons-io'
        exclude module: 'commons-lang3'
    }

    implementation("org.slf4j:slf4j-api:${slf4j_version}")
    implementation("ch.qos.logback:logback-classic:${logback_version}") {
        exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    }
    implementation("fr.raksrinana:utils.config:${raksrinana_utils_version}")

    implementation("org.apache.commons:commons-math3:${commons_math3_version}")
    implementation("org.apache.commons:commons-lang3:${commons_lang3_version}")
    implementation("info.picocli:picocli:${picocli_version}")
}

repositories {
    maven {
        url = 'https://maven.pkg.github.com/RakSrinaNa/JavaUtils/'
        credentials {
            username buildProperties.secrets['GITHUB_USER'].string
            password buildProperties.secrets['GITHUB_TOKEN'].string
        }
    }
    jcenter()
    mavenCentral()
}

processResources {
    expand project.properties
}

application {
    mainModule = project.moduleName
    mainClass = project.mainClass
}

java {
    modularity.inferModulePath.set(true)
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

shadowJar {
    archivesBaseName = project.name
    archiveClassifier.set('shaded')
    archiveVersion.set('')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
}

tasks.withType(Wrapper) {
    gradleVersion = project.wrapper_version
}
